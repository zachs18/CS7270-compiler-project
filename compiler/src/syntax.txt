list[x] = ( x ( ',' x )* ','? )?

FILE: ITEM* eof

ITEM: FNITEM | STATICITEM

FNITEM: 'extern'? 'fn' ident '(' list[FN_PARAM] ')' (BLOCK | ';')

STATICITEM: 'extern'? 'static' 'mut'? ident ':' TYPE ( '=' EXPRESSION )? ';'

FN_PARAM: PATTERN ':' TYPE
// TODO: maybe `...` for variadics

PATTERN:
 | '_'
 | ident
 | 'mut' ident
 | integer
 | '(' list[PATTERN_WITH_ALT] ')'
 | '[' list[PATTERN_WITH_ALT] ']'

PATTERN_WITH_ALT: '|'? PATTERN ('|' PATTERN)*

TYPE:
 | ident
 | '!'
 | '[' TYPE (';' integer)? ']'
 | '(' list[TYPE] ')'
 | '*' ('const' | 'mut') TYPE

BLOCK: '{' STATEMENT* EXPRESSION? '}'

STATEMENT:
 | EXPRESSION ';'
 | LET_STATEMENT
// TODO: other statements?

LET_STATEMENT: 'let' PATTERN ( ':' TYPE )? ( '=' EXPRESSION )? ';'

EXPRESSION: EXPR_BLOCK | EXPR_NO_BLOCK

EXPR_NO_BLOCK:
 | `break` EXPR_NO_BLOCK?
 | `return` EXPR_NO_BLOCK?
 | OPERATOR_EXPRESSION
// TODO: other expressions

EXPR_BLOCK:
 | BLOCK
 | IF_EXPR
 | MATCH_EXPR
 | LOOP_EXPR

IF_EXPR: 'if' EXPR_NO_BLOCK BLOCK ( 'else' 'if' EXPR_NO_BLOCK BLOCK )* ('else' BLOCK)?

MATCH_EXPR: 'match' EXPR_NO_BLOCK '{' list[MATCH_ARM] '}'

MATCH_ARM: PATTERN_WITH_ALT '=>' EXPRESSION

LOOP_EXPR:
 | 'while' EXPR_NO_BLOCK BLOCK
 | 'for' PATTERN 'in' EXPR_NO_BLOCK BLOCK
 | 'loop' BLOCK

// Note: operator precedence is handled later (not in the syntax itself)
OPERATOR_EXPRESSION:
 | CAST_EXPR ( BINOP CAST_EXPR )*

BINOP: ARITH_BINOP | COMPARISON_BINOP | ASSIGN_BINOP

// Note: longer punct-sequences need to be checked first
ARITH_BINOP: '+' | '-' | '*' | '/' | '%' | '&&' | '||' | '&' | '|' | '^' | '>>' | '<<'
COMPARISON_BINOP: '==' | '<=' | '>=' | '!=' | '<' | '>'
ASSIGN_BINOP: '=' | '+=' | '-=' | '*=' | '/=' | '%=' | '&&=' | '||=' | '&=' | '|=' | '^=' | '>>=' | '<<='
RANGE_BINOP: '..' | '..='

// Separate the unary operators by precedence:
// (return and break are lower precedence than binary operators, so are implemented in EXPR_NO_BLOCK)
// CAST_EXPR because `as` casts bind loosest (lowest precedence)
// then PREFIX_UNARY_EXPR for the prefix operators with medium precedence
// then POSTFIX_UNARY_EXPR for the postfix operators with high precedence

CAST_EXPR: PREFIX_UNARY_EXPR ( 'as' TYPE )*

PREFIX_UNARY_EXPR:
 | '-' PREFIX_UNARY_EXPR
 | '!' PREFIX_UNARY_EXPR
 | '*' PREFIX_UNARY_EXPR
 | '&' 'mut'? PREFIX_UNARY_EXPR
 | POSTFIX_UNARY_EXPR

POSTFIX_UNARY_EXPR:
 | integer
 | ident
 | 'true'
 | 'false'
 | '_'
 | '(' list[EXPRESSION] ')'
 | '[' list[EXPRESSION] ']'
 | POSTFIX_UNARY_EXPR '[' EXPRESSION ']'
 | POSTFIX_UNARY_EXPR '(' list[EXPRESSION] ')'
